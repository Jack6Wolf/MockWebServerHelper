import groovy.io.FileType

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.orhanobut.sample"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

task generateFixtures(group: 'fixtures') {
    def directory = projectDir.path + '/src/test/java'
    new File(directory + '/fixtures').mkdir()
    def path = directory + "/fixtures/Fixtures.java"

    def builder = '' << ''
    builder.append("package fixtures;\n\n")
    builder.append("public class Fixtures {\n\n")
    builder.append("  private Fixtures() {\n")
    builder.append("    //no instance\n")
    builder.append("  }\n\n")

    def resources = android.sourceSets.test.resources.srcDirs.getAt(0)
    if (resources != null) {
        resources.eachDirMatch("fixtures") { dir ->
            def fixturesFile = dir
            fixturesFile.eachFile(FileType.FILES) { file ->
                if (file.name.endsWith(".yaml")) {
                    String fileName = file.name.split('\\.')[0]
                    builder.append("  public static final String ")
                            .append(fileName.toUpperCase())
                            .append(" = ")
                            .append('\"')
                            .append(fileName)
                            .append('\";\n')
                }
            }
        }
    }
    builder.append("}\n")
    println builder.toString()
    new File(path).write(builder.toString())
}

preBuild.dependsOn generateFixtures

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:2.4.0'
    testImplementation 'org.mockito:mockito-core:1.9.5'
    testImplementation 'com.squareup.okhttp3:okhttp:3.7.0'
    testImplementation('com.squareup.okhttp3:mockwebserver:3.7.0')
    testImplementation project(':mockwebserverplus')
}

